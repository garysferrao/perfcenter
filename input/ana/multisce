//Experiment 1: different procesor speeds
//check
//variable definition block
variable
nusr 4
ccount 10
end

//devices are defined below
device
cpu
end

//lan definition block
lan
lan1
end

//hosts definition blocks

host machine1[1]
cpu count ccount
cpu buffer 99999
cpu schedP fcfs
end

host machine2[1]
cpu count 3
cpu buffer 99999
cpu schedP fcfs

end

//tasks definition block
task send_to_auth
cpu servt 0.030
end

task send_to_imap 
cpu servt 0.020
end
 

task change_to_html
cpu servt 0.010
end 

task verify_passwd 
cpu servt 0.010
end 

task verify_session 
cpu servt 0.010
end

task list_message
cpu servt 0.025
end 

task read_message 
cpu servt 0.020
end 

task delete_message 
cpu servt 0.015
end

//servers definition
server web
thread count 2
thread buffer 99999
thread schedP fcfs
task send_to_auth
task send_to_imap
task change_to_html
end


server auth
thread count 4
thread buffer 99999
thread schedP fcfs
task verify_passwd
task verify_session
end

server imap
thread count 4
thread buffer 99999
thread schedP fcfs
task list_message
task read_message
task delete_message
end

//server deployed onto hosts
deploy web machine1[1]
deploy auth machine2[1]
deploy imap machine2[1]


//host deployed onto lans
deploy machine1[1] lan1
deploy machine2[1] lan1
 

//scenario definitions
scenario Login prob 0.5
    send_to_auth  verify_passwd 200 SYNC
    verify_passwd  send_to_imap 200  
    send_to_imap list_message 200 SYNC 
    list_message  change_to_html 2000
end

scenario Read prob 0.5
    send_to_auth  verify_session 200 SYNC
    verify_session  send_to_imap 200 
    send_to_imap read_message 200 SYNC 
    read_message  change_to_html 2000
end



loadparams
   noofusers    		nusr
   thinktime    		exp(3) 
end



//This block has parameters for simulation

modelparams
    method analytical
    type closed
end


while(nusr<=15)	   
print nusr + " " +respt(Login) + " "+respt(Read)+" "+ util(machine1[1]:web)+" "+tput(machine1[1]:web)+" "+ util(machine2[1]:auth)+" "+tput(machine2[1]:auth)+" "+ util(machine2[1]:imap)+" "+tput(machine2[1]:imap)
 nusr = nusr+1
end
